#!/bin/csh -f
#
# There is a file in /csafe_noexport/web/bin that will pick this
# up every hour on the hour
#

if ("$1" == "" || "$2" == "" || "$3" == "") then
  echo "usage: makepages WEBTOP PSETOP URLTOP"
  echo "WEBTOP is the root of the web directory"
  echo "PSETOP is the location of the PSE/src directory"
  echo "URLTOP is the web location of the pages"
  exit 1
endif
set webtop=$1
set psetop=$2
set urltop=$3
set tmptop=$psetop/..

cd $psetop
set docdirs = `find . -type d -name doc -print`
foreach t ($docdirs)
  set dir = `dirname $t`
  mkdir -p $webtop/$dir
  # *.* won't pick up CVS, but will get most important things
  cp $t/*.* $webtop/$dir
end

#
# The complicated awk is to extract the directory name only.  It is harder
# than it should be because awk doesn't have an rindex function.  Oh well.
# The first uniq isn't necessary, but it trims the list substantially
# before sorting
#
set hdirs = `find . -type f -name "*.h" -print | awk '{x=$1;s=0;l=1;while(l!=0){l=index(x,"/");x=substr(x,l+1,length(x)-l);s+=l;} print substr($1,1,s);}' | uniq | sort | uniq`

set cfile=$tmptop/cocoon_config
cat >$cfile <<EOF
customize
        usetable
        logo            /images/csafe.jpg
        showprivates
        nobacktotop
        backcolor white
        ignoredirective PSECORESHARE
        ignoredirective SCICORESHARE
	numkeycolumns 3
end_customize

webroot  $webtop
urlroot  $urltop

#
# libary <name> <source_dir> <web_sub_dir>
#
EOF

foreach t ($hdirs)
set cleanname=`echo $t | sed -e 's,./,,' -e 's,/$,,'`
if (`grep ^$cleanname\$ src/scripts/cocoon.skip` == "") then
set libname=`echo $cleanname | sed -e 's,/,::,g'`
set webname=$cleanname
if("$libname" == "") then
  set libname="PSE"
  set webname="."
endif
echo "library $libname $psetop/$cleanname $webname" >> $cfile
# If there is a doc/cocoon.cust file, use it.  Otherwise use the default
# that specifies no sentinels
if (-f $t/doc/cocoon.cust) then
  cat $t/doc/cocoon.cust >> $cfile
echo "using $t/doc/cocoon.cust"
else
cat >>$cfile <<EOF
 customize
     nosentinels
 end_customize
EOF
endif
echo >> $cfile
endif
end

time cocoon $cfile
if ($status != 0) then
  echo "COCOON FAILED using config file: "
  cat $cfile
endif

rm -f $cfile
